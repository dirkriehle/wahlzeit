
buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.google.appengine:gradle-appengine-plugin:+'
    }
}

repositories {
    maven {
        url 'https://maven-central.storage.googleapis.com'
    }
    jcenter()
    mavenCentral()
}

apply plugin: 'java'
apply plugin: 'war'
apply plugin: 'appengine'

wrapper {
	gradleVersion = '4.10'
}

// check if JAVA_HOME is set, otherwise build tasks will fail
gradle.taskGraph.whenReady {
    graph ->
    if (System.env.'JAVA_HOME' == null) {
        throw new GradleException("JAVA_HOME not set. Please set it, otherwise this task can not be executed.")
    }
    else {
        println "JAVA_HOME = " + System.env.'JAVA_HOME'
    }
}

dependencies {
    // https://cloud.google.com/appengine/docs/standard/java/release-notes

    providedCompile group: 'javax.servlet', name: 'servlet-api', version:'2.5'
    compile 'com.google.appengine:appengine:+'

    appengineSdk 'com.google.appengine:appengine-java-sdk:+'

    compile 'com.google.appengine:appengine-api-1.0-sdk:+'
    compile 'com.google.appengine.tools:appengine-gcs-client:+'
    compile 'com.googlecode.objectify:objectify:5.1.21'
    //compile 'javax.servlet:servlet-api:2.5' // GAE works with only with 2.5
    compile 'commons-fileupload:commons-fileupload:1.3.3'

    // testing dependencies
    testCompile 'com.google.appengine:appengine-testing:+'
    testCompile 'com.google.appengine:appengine-api-stubs:+'
    testCompile 'com.google.appengine:appengine-api-labs:+'
    testCompile 'com.google.appengine:appengine-tools-sdk:+'
    testCompile 'junit:junit:4.+'
    testCompile 'org.mockito:mockito-core:2.22.0'
}

test {
    filter {
        includeTestsMatching "org.wahlzeit.*"
    }
    testLogging {
            events "passed", "skipped", "failed"

            afterSuite { desc, result ->
                if (!desc.parent) {
                    println "Tests run: ${result.testCount}, " +
                            "Failures: ${result.failedTestCount}, " +
                            "Skipped: ${result.skippedTestCount}"
                }
            }
        }
}

appengine {
    httpPort = 8080
    downloadSdk = true
    jvmFlags = ['-Xdebug', '-Xrunjdwp:transport=dt_socket,server=y,suspend=n,address=8000']

    appcfg {
        oauth2 = true
        noCookies = false
    }
}

// Google App Engine Standard Environment supports Java 8 only (as from September 2018, https://cloud.google.com/appengine/docs)
sourceCompatibility = JavaVersion.VERSION_1_8 // Java version compatibility to use when compiling Java source.
targetCompatibility = JavaVersion.VERSION_1_8 // Java version to generate classes for.
