buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.google.appengine:gradle-appengine-plugin:+'
    }
}

repositories {
    maven {
        url 'https://maven-central.storage.googleapis.com'
    }
    jcenter()
    mavenCentral()
}

apply plugin: 'java'
apply plugin: 'war'
/* if there is a warning regarding deprecated behaviour caused by 'DefaultSourceSetOutput.getClassesDir'
 * see https://github.com/GoogleCloudPlatform/gradle-appengine-plugin/issues/290 */
apply plugin: 'appengine'

// check if JAVA_HOME is set, otherwise build tasks will fail
gradle.taskGraph.whenReady {
    graph ->
        if (System.env.'JAVA_HOME' == null) {
            throw new GradleException("JAVA_HOME not set. Please set it, otherwise this task can not be executed.")
        } else {
            println "JAVA_HOME = " + System.env.'JAVA_HOME'
        }
}

repositories {
    mavenCentral()
}

dependencies {
    // https://cloud.google.com/appengine/docs/standard/java/release-notes

    providedCompile group: 'javax.servlet', name: 'servlet-api', version: '2.5'
    compile 'com.google.appengine:appengine:+'

    appengineSdk 'com.google.appengine:appengine-java-sdk:+'

    compile 'com.google.appengine:appengine-api-1.0-sdk:+'
    compile 'com.google.appengine.tools:appengine-gcs-client:+'
    compile 'com.googlecode.objectify:objectify:5.1.21'
    //compile 'javax.servlet:servlet-api:2.5' // GAE works with only with 2.5
    compile 'commons-fileupload:commons-fileupload:1.3.3'

    // testing dependencies
    testCompile 'com.google.appengine:appengine-testing:+'
    testCompile 'com.google.appengine:appengine-api-stubs:+'
    testCompile 'com.google.appengine:appengine-api-labs:+'
    testCompile 'com.google.appengine:appengine-tools-sdk:+'
    testCompile 'junit:junit:4.+'
    testCompile 'org.mockito:mockito-core:2.11.0'
}

test {
    filter {
        includeTestsMatching "org.wahlzeit.*"
    }
}

appengine {
    httpPort = 8080
    downloadSdk = true
    jvmFlags = ['-Xdebug', '-Xrunjdwp:transport=dt_socket,server=y,suspend=n,address=8000']

    appcfg {
        oauth2 = true
        noCookies = false
    }
}

sourceCompatibility = 1.8 // Java version compatibility to use when compiling Java source.
targetCompatibility = 1.8 // Java version to generate classes for.
