apply plugin: 'java'
apply plugin: 'war'
apply plugin: 'appengine'

// Google App Engine Standard Environment supports Java 7 only (as from August 2016, https://cloud.google.com/appengine/docs)
sourceCompatibility = 1.7 // Java version compatibility to use when compiling Java source.
targetCompatibility = 1.7 // Java version to generate classes for.

buildscript {
    ext {
        gaeVersion = '1.9.42'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.google.appengine:gradle-appengine-plugin:' + gaeVersion
    }
}

// check if JAVA_HOME is set, otherwise build tasks will fail
gradle.taskGraph.whenReady {
    graph ->
    if (System.env.'JAVA_HOME' == null) {
        throw new GradleException("JAVA_HOME not set. Please set it, otherwise this task can not be executed.")
    }
    else {
        println "JAVA_HOME = " + System.env.'JAVA_HOME'
    }
}

repositories {
    mavenCentral()
}

dependencies {
    appengineSdk 'com.google.appengine:appengine-java-sdk:' + gaeVersion

    compile 'com.google.appengine:appengine-api-1.0-sdk:' + gaeVersion
    compile 'com.google.appengine.tools:appengine-gcs-client:0.6'
    compile 'com.googlecode.objectify:objectify:5.1.5'
    compile 'javax.servlet:servlet-api:2.5' // GAE works with only with 2.5
    compile 'commons-fileupload:commons-fileupload:1.2'

    // testing dependencies
    testCompile 'com.google.appengine:appengine-testing:' + gaeVersion
    testCompile 'com.google.appengine:appengine-api-stubs:' + gaeVersion
    testCompile 'com.google.appengine:appengine-api-labs:' + gaeVersion
    testCompile 'com.google.appengine:appengine-tools-sdk:' + gaeVersion
    testCompile 'junit:junit:4.+'
    testCompile 'org.mockito:mockito-core:1.10.19'
}

test {
    // adjust this filter to your needs
}

appengine {
    httpPort = 8080
    downloadSdk = true
    jvmFlags = ['-Xdebug', '-Xrunjdwp:transport=dt_socket,server=y,suspend=n,address=8000']

    appcfg {
        oauth2 = true
        noCookies = false
    }
}
